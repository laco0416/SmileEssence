apply plugin: 'com.android.application'

def consumerKey, consumerSecret, accessToken, accessTokenSecret

if (System.getenv("CONSUMER_KEY") != null) {
    println("### Environment variables are found. ###")
    consumerKey = System.getenv("CONSUMER_KEY")
    consumerSecret = System.getenv("CONSUMER_SECRET")
    accessToken = System.getenv("ACCESS_TOKEN")
    accessTokenSecret = System.getenv("ACCESS_TOKEN_SECRET")
} else {
    Properties props = new Properties()
    try {
        props.load(file('./tokens.properties').newDataInputStream())
        consumerKey = props.get("consumerKey")
        consumerSecret = props.get("consumerSecret")
        accessToken = props.get("accessToken")
        accessTokenSecret = props.get("accessTokenSecret")
    } catch (Exception ignored) {
        throw new GradleException("Missing tokens.properties, check the tokens.properties file.");
    }
}

android {
    compileSdkVersion 17
    buildToolsVersion '22.0.1'
    defaultConfig {
        applicationId "net.lacolaco.smileessence"
        minSdkVersion 17
        maxSdkVersion 19
        targetSdkVersion 19
        versionCode 25
        versionName "1.0.11.1"
        buildConfigField "String", "CONSUMER_KEY", "\"${consumerKey}\""
        buildConfigField "String", "CONSUMER_SECRET", "\"${consumerSecret}\""
        buildConfigField "String", "ACCESS_TOKEN", "\"${accessToken}\""
        buildConfigField "String", "ACCESS_TOKEN_SECRET", "\"${accessTokenSecret}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //    compile 'com.android.support:appcompat-v7:22.1.0'
    compile 'com.android.support:support-v13:18.0.0'
    // volley
    compile project(':modules/volley')
    // crouton
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        // exclusion is not necessary, but generally a good idea.
        exclude group: 'com.google.android', module: 'support-v4'
    }
    // pull to refresh
    compile 'com.navercorp.pulltorefresh:library:3.2.0@aar'
    // t4j
    compile 'org.twitter4j:twitter4j-core:4.0.3'
    compile 'org.twitter4j:twitter4j-media-support:4.0.3'
    compile 'org.twitter4j:twitter4j-stream:4.0.3'
    // twitter-text
    compile 'com.twitter:twitter-text:1.6.1'
    // guava
    compile 'com.google.guava:guava:18.0'

}
